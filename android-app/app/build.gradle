plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
    id 'org.jetbrains.kotlin.plugin.compose'
}

// local.properties에서 서버 URL 읽기
def localProps = new Properties()
def localPropsFile = rootProject.file('local.properties')
if (localPropsFile.exists()) {
    localProps.load(localPropsFile.newDataInputStream())
}
def serverUrl = localProps.getProperty('SERVER_URL', 'http://10.0.2.2:8000/')

android {
    namespace = 'com.example.plant'
    compileSdk 36

    defaultConfig {
        applicationId "com.guru.floripedia"
        minSdk 24
        targetSdk 36
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "BASE_URL", "\"${serverUrl}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            zipAlignEnabled true
        }
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    
    kotlinOptions {
        jvmTarget = '11'
    }
    
    buildFeatures {
        viewBinding = true
        compose = true
        buildConfig = true
    }

    // 16 KB 페이지 크기 기기와의 호환성을 위한 설정
    packaging {
        jniLibs {
            // .so 파일들을 압축하지 않고 16 KB 경계에 맞게 정렬 (AGP 8.3+ 권장 방식)
            useLegacyPackaging = false
        }
    }
    buildToolsVersion '35.0.0'
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    //Camera (1.4.0+ supports 16KB page alignment)
    def camerax_version = "1.4.1"
    implementation("androidx.camera:camera-core:${camerax_version}")
    implementation("androidx.camera:camera-camera2:${camerax_version}")
    implementation("androidx.camera:camera-lifecycle:${camerax_version}")
    implementation("androidx.camera:camera-view:${camerax_version}")

    // ViewPager2
    implementation("com.tbuonomo:dotsindicator:5.0")
    // ViewPager2
    implementation("androidx.viewpager2:viewpager2:1.1.0")
    // FlexboxLayout
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    
    // Networking
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.11.0'
    
    // Image Loading
    implementation 'io.coil-kt:coil:2.4.0'
    implementation 'io.coil-kt:coil-compose:2.4.0'
    
    // Firebase (Storage & Auth)
    implementation platform('com.google.firebase:firebase-bom:34.8.0')
    implementation 'com.google.firebase:firebase-storage'
    implementation 'com.google.firebase:firebase-auth'

    // Google Sign-In
    implementation 'com.google.android.gms:play-services-auth:21.0.0'

    // Coroutines for Firebase
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.7.3'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.10.0'
    implementation 'androidx.activity:activity-compose:1.12.2'
    implementation platform('androidx.compose:compose-bom:2024.09.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.11.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation platform('androidx.compose:compose-bom:2024.09.00')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}
